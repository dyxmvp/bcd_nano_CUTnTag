import itertools

include: "Snakefile_cellranger"

configfile: workflow.basedir + '/../config/config.yaml'


samples_list  = list(config['samples'].keys())
# ['bcdCT_MB21_02', 'bcdCT_MB21_03']

barcodes_dict = {sample: config['samples'][sample]['barcodes'] for sample in samples_list}
# {'bcdCT_MB21_02': {'ATAC': 'TATAGCCT', 'H3K27ac': 'ATAGAGGC', 'H3K27me3': 'CCTATCCT'}, 'bcdCT_MB21_03': {'H3K27ac': 'ATAGAGGC', 'H3K27me3': 'CCTATCCT'}}

# Rename a modality if it has "_" in its name, substitute "_" with "-"
for sample in samples_list:
    for modality in barcodes_dict[sample].keys():
        if "_" in modality:
            new_modality = modality.replace("_","-")
            sys.stderr.write("*** Warning: Renaming modality name {old} to {new}; Can't have \"_\" in modality name *** \n".format(old=modality,new=new_modality))
            barcodes_dict[sample][new_modality] = barcodes_dict[sample][modality]
            del(barcodes_dict[sample][modality])

antibodies_list = list(set(itertools.chain(*[barcodes_dict[sample].keys() for sample in barcodes_dict.keys()])))
# ['H3K27me3', 'ATAC', 'H3K27ac']

# Find combinations of modalities
# These are only realistic combinations of modalities
modalities_combinations =  [itertools.combinations(list(barcodes_dict[sample].keys()),i) for sample in samples_list for i in range(2,1+len(barcodes_dict[sample].keys()))]
modalities_combinations = [list(x) for x in list(set(itertools.chain(*modalities_combinations)))]
# [('H3K27ac', 'H3K27me3'), ('ATAC', 'H3K27me3'), ('ATAC', 'H3K27ac')]
print(modalities_combinations)



shell.executable("/bin/bash")
shell.prefix("source ~/.bash_profile; conda activate " + config['general']['conda_env']  + " ; ")

def get_input_for_multiple_modalities(combination, barcodes_dict, binwidth):
    combination = combination.split("_")
    files = ["results/single_modality/{modality}/seurat_{binwidth}/Seurat_object_clustered.Rds".format( modality = modality, binwidth = binwidth) for modality in combination]

    return(files)

rule all_demultiplex:
    input:
        ############################
        # Single modality analysis #
        ############################
        expand('results/single_modality/{modality}/seurat_{binwidth}/Seurat_object.Rds', modality = antibodies_list,binwidth=5000),         # Merge
        expand('results/single_modality/{modality}/seurat_{binwidth}/Seurat_object_clustered.Rds',modality=antibodies_list,binwidth=5000),  # Cluster
        expand('results/single_modality/{modality}/seurat_{binwidth}/integration/integration_RNA.Rds', modality=antibodies_list, binwidth=5000), # Integration with scRNA-seq

        ################################
        # Multiple modalities analysis #
        ################################
        [expand('results/multiple_modalities/{combination}/seurat_{binwidth}/Seurat_object.Rds', combination = "_".join(combination), binwidth=5000) for combination in modalities_combinations],
        [expand('results/multiple_modalities/{combination}/seurat_{binwidth}/Seurat_object_clustered.Rds', combination = "_".join(combination), binwidth=5000) for combination in modalities_combinations],
        [expand('results/multiple_modalities/{combination}/seurat_{binwidth}/integration/integration_RNA.Rds',combination="_".join(combination),binwidth=5000) for combination in modalities_combinations],

rule merge_same_modality:
    input:
        lambda wildcards: ['results/{sample}/{modality}_{barcode}/seurat/bin_{binwidth}/Seurat_object.Rds'.format( \
            sample=sample, \
            modality=wildcards.modality, \
            barcode = barcodes_dict[sample][wildcards.modality], \
            binwidth=wildcards.binwidth) \
                for sample in barcodes_dict.keys() if wildcards.modality in barcodes_dict[sample].keys()]
    output:
        'results/single_modality/{modality}/seurat_{binwidth}/Seurat_object.Rds',
    shell:
        'Rscript ' + os.path.dirname(workflow.basedir) + '/scripts/merge_objects.R ' + '-i {input} -o {output}'


rule cluster:
    input:
        'results/{x}/{modality}/seurat_{binwidth}/Seurat_object.Rds'
    output:
        'results/{x}/{modality}/seurat_{binwidth}/Seurat_object_clustered.Rds'
    params:
        script = os.path.dirname(workflow.basedir) + '/scripts/UMAP_cluster.R'
    shell:
        "Rscript {params.script} -i {input} -o {output}"


rule merged_diff_modality:
    input:
        lambda wildcards: get_input_for_multiple_modalities(combination=wildcards.combination,\
                                                            barcodes_dict=barcodes_dict, \
                                                            binwidth = wildcards.binwidth)
    output:
        'results/multiple_modalities/{combination}/seurat_{binwidth}/Seurat_object.Rds'

    shell:
        'Rscript ' + os.path.dirname(workflow.basedir) + '/scripts/merge_modalities2.R -i {input} -m {wildcards.combination} -o {output}'



rule integrate_with_scRNA:
    input:
        seurat = 'results/{x}/{modality}/seurat_{binwidth}/Seurat_object_clustered.Rds',
        rna    = '/data/proj/GCB_MB/single-cell-CUT-Tag/nbiotech_paper/analysis/results/Sten_RNA/clustering/01.clustering_20000cells.Rds', # TODO fix path here
    output:
        'results/{x}/{modality}/seurat_{binwidth}/integration/integration_RNA.Rds'
    params:
        script = os.path.dirname(workflow.basedir) + '/scripts/integrate_CT_RNAseq.R'
    shell:
        "Rscript {params.script} -i {input.seurat} -r {input.rna} -o {output}"
